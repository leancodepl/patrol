buildscript {
    ext.kotlin_version = "1.7.22"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.1.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.android.library"
    id "kotlin-android"
    id "com.google.protobuf" version "0.8.19"
    id "org.jlleitschuh.gradle.ktlint" version "11.1.0"
}

ktlint {
    filter {
        exclude("**/contracts/**")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

group "pl.leancode.patrol"
version "1.0-SNAPSHOT"

android {
    compileSdk 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
    }

    defaultConfig {
        minSdk 19
    }

    dependencies {
        // TODO: These dependencies should not be added to release builds.
        // https://github.com/flutter/flutter/issues/56591
        api "junit:junit:4.13.2"

        api "androidx.test:runner:1.5.1"
        api "androidx.test.espresso:espresso-core:3.5.0"
        api "androidx.test.uiautomator:uiautomator:2.2.0"

        implementation "com.google.protobuf:protobuf-kotlin-lite:3.21.7"
        implementation "io.grpc:grpc-protobuf-lite:1.49.2"
        implementation "io.grpc:grpc-stub:1.49.2"
        implementation "io.grpc:grpc-kotlin-stub:1.3.0"
        implementation "io.grpc:grpc-okhttp:1.49.2"
        implementation "javax.annotation:javax.annotation-api:1.3.2"

        testImplementation "org.jetbrains.kotlin:kotlin-test"
    }
}

// Test Logging
tasks.withType(Test).configureEach {
    testLogging {
        events "passed", "skipped", "failed"
    }
}
