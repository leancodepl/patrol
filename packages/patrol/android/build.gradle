group "pl.leancode.patrol"
version "1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = "1.8.22"
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.4.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:11.5.0"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: "kotlinx-serialization"

ktlint {
    filter {
        exclude("**/contracts/**")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

android {
    namespace "pl.leancode.patrol"
    compileSdk 33

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
    }

    defaultConfig {
        minSdk 21
    }

    dependencies {
        // TODO: These dependencies should not be added to release builds.
        // https://github.com/flutter/flutter/issues/56591
        api "junit:junit:4.13.2"

        api "androidx.test:runner:1.5.1"
        api "androidx.test.espresso:espresso-core:3.5.0"
        api "androidx.test.uiautomator:uiautomator:2.2.0"

        implementation platform("org.http4k:http4k-bom:5.7.4.0")
        implementation "org.http4k:http4k-core"
        implementation "org.http4k:http4k-client-okhttp"
        implementation "org.http4k:http4k-server-ktorcio"
        implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-guava:1.5.2"

        testImplementation "org.jetbrains.kotlin:kotlin-test"
    }
}

// Test Logging
tasks.withType(Test).configureEach {
    testLogging {
        events "passed", "skipped", "failed"
    }
}
