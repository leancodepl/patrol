group "pl.leancode.patrol"
version "1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = "1.9.20"
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.4.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:11.5.0"
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "org.jlleitschuh.gradle.ktlint"

ktlint {
    filter {
        exclude("**/contracts/**")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

android {
    namespace "pl.leancode.patrol"
    compileSdk 33

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
    }

    defaultConfig {
        minSdk 21
        buildConfigField("String", "PATROL_APP_PORT", project.hasProperty('app-server-port') ? '"' + project.getProperty('app-server-port') + '"' : '""')
        buildConfigField("String", "PATROL_TEST_PORT", project.hasProperty('test-server-port') ? '"' + project.getProperty('test-server-port') + '"' : '""')
    }

    buildFeatures {
        buildConfig true
    }

    dependencies {
        // TODO: These dependencies should not be added to release builds.
        // https://github.com/flutter/flutter/issues/56591
        api "junit:junit:4.13.2"

        api "androidx.test:runner:1.5.1"
        api "androidx.test.espresso:espresso-core:3.5.0"
        api "androidx.test.uiautomator:uiautomator:2.2.0"

        // We need to downgrade http4k-bom to 4.48.0.0 because apps with kotlin version (ext.kotlin_version) lower than 1.8 
        // end up with compile-time errors. We can find more details in a similar problem:
        // https://stackoverflow.com/questions/67699823/module-was-compiled-with-an-incompatible-version-of-kotlin-the-binary-version-o
        implementation platform("org.http4k:http4k-bom:4.48.0.0")
        implementation "org.http4k:http4k-core"
        implementation "com.squareup.okhttp:okhttp:2.7.5" // See https://github.com/square/okhttp/issues/8031
        implementation "org.http4k:http4k-server-ktorcio"
        implementation "com.google.code.gson:gson:2.10.1"

        testImplementation "org.jetbrains.kotlin:kotlin-test"
    }
}

// Test Logging
tasks.withType(Test).configureEach {
    testLogging {
        events "passed", "skipped", "failed"
    }
}
